<?xml version="1.0"?>
<!-- 
/*
 * Copyright 2001-2004 The Apache Software Foundation.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *      http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 -->

<document>

 <properties>
  <title>PortletBridge Portlet XSL</title>
 </properties>

<body>
<section name="Sample XSL">
<p>
The PortletBridge portlet has a stylesheet preference which can be used
to set up custom XSLT for a particular instance of a portlet. Below is
what the default XSLT looks like. This can be edited and set as the preference
to alter how the portlet renders downstream sites.
</p>
<p>
<source>
<![CDATA[
<?xml version="1.0" encoding="ISO-8859-1"?>
<xsl:stylesheet version="1.0"
   xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
   xmlns:bridge="java:org.portletbridge.portlet.BridgeFunctions"
   extension-element-prefixes="bridge">
   
   <!-- @author rickard -->
   <!-- @author jamie -->

   <xsl:output method="html"
      encoding="UTF-8"
      indent="yes"
      standalone="no"
      omit-xml-declaration="yes"/>

   <xsl:param name="bridge"/>

   <!-- Fetch some info from head, and all of body -->
   <xsl:template match="/HTML">
      <xsl:apply-templates select="HEAD"/>
      <xsl:apply-templates select="BODY"/>
   </xsl:template>

   <xsl:template match="/HTML/HEAD">
      <xsl:apply-templates select="STYLE"/>
      <xsl:apply-templates select="LINK[@rel='stylesheet']"/>
      <xsl:apply-templates select="SCRIPT"/>
   </xsl:template>

   <xsl:template match="/HTML/BODY">
      <xsl:apply-templates select="node()"/>
   </xsl:template>

   <!-- Rewrite links -->
   <xsl:template match="A/@href">
      <xsl:attribute name="href">
         <xsl:value-of select="bridge:link($bridge, .)"/>
      </xsl:attribute>
   </xsl:template>

   <xsl:template match="A/@onclick">
      <xsl:attribute name="onclick">
         <xsl:value-of select="bridge:script($bridge, .)"/>
      </xsl:attribute>
   </xsl:template>

   <!-- Rewrite image references -->
   <xsl:template match="IMG/@src">
      <xsl:attribute name="src">
         <xsl:value-of select="bridge:link($bridge, .)"/>
      </xsl:attribute>
   </xsl:template>

   <!-- Rewrite imagemap references -->
   <xsl:template match="MAP/AREA/@href">
      <xsl:attribute name="href">
         <xsl:value-of select="bridge:link($bridge, .)"/>
      </xsl:attribute>
   </xsl:template>

   <!-- Copy style tags from head -->
   <xsl:template match="/HTML/HEAD/STYLE">
      <xsl:copy>
         <xsl:apply-templates select="@*"/>

         <xsl:value-of select="bridge:style($bridge, .)"/>
      </xsl:copy>
   </xsl:template>

   <!-- Copy script tags from head -->
   <xsl:template match="/HTML/HEAD/SCRIPT[@src]">
      <xsl:copy>
         <xsl:apply-templates select="@*"/>
         <xsl:attribute name="src">
            <xsl:value-of select="bridge:link($bridge, @src)"/>
         </xsl:attribute>
      </xsl:copy>
   </xsl:template>

   <xsl:template match="/HTML/HEAD/SCRIPT/text()">
      <xsl:value-of select="bridge:script($bridge, .)"/>
   </xsl:template>

   <!-- Convert link tags in head to style tags -->
   <xsl:template match="/HTML/HEAD/LINK">
      <style type="text/css">@import "
         <xsl:value-of select="bridge:link($bridge, @href)"/>";
      </style>
   </xsl:template>

   <xsl:template match="FORM/@action">
      <xsl:attribute name="action">
         <xsl:value-of select="bridge:link($bridge, .)"/>
      </xsl:attribute>
   </xsl:template>

   <!-- Identity template -->
   <xsl:template match="@*|*">
      <xsl:copy>
         <xsl:apply-templates select="@*|node()"/>
      </xsl:copy>
   </xsl:template>
</xsl:stylesheet>
]]>
</source>
</p>
</section>
</body>
</document>
